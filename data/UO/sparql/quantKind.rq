PREFIX uo: <http://purl.obolibrary.org/obo/uo#>
PREFIX pato: <http://purl.obolibrary.org/obo/pato#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT  ?quantKind 
        ?label
        ?labelLang 
        ?parent
WHERE {
  ?quantKind  rdfs:subClassOf obo:PATO_0000001;
              rdfs:label      ?label.
  BIND( LANG(?label) AS ?labelLang )
              
  FILTER EXISTS {?quantKind oboInOwl:inSubset ?slim}
  FILTER NOT EXISTS {?quantKind oboInOwl:inSubset pato:value_slim}
  OPTIONAL {?quantKind rdfs:subClassOf ?parent.
    ?parent rdfs:subClassOf obo:PATO_0000001.
    FILTER NOT EXISTS {
      ?sub rdfs:subClassOf ?parent.
      ?quantKind rdfs:subClassOf ?sub.
      FILTER (?quantKind != ?sub && ?sub != ?parent)
    }
    FILTER (?quantKind != ?parent)
  }
}